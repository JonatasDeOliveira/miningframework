package main.arguments

@Grab('com.xlson.groovycsv:groovycsv:1.3')
import static groovy.io.FileType.DIRECTORIES
import static com.xlson.groovycsv.CsvParser.parseCsv

import main.project.*;

class InputParser {

    static ArrayList<Project> getProjectList(String inputPath) {
        ArrayList<Project> projectList = new ArrayList<Project>()

        String projectsFile = new File(inputPath).getText()
        def iterator = parseCsv(projectsFile)
        for (line in iterator) {
            String name = line[0]
            String path = line[1]

            boolean relativePath
            try {
                relativePath = line[2].equals("true")
            } catch(ArrayIndexOutOfBoundsException e) {
                relativePath = false
            }

            if(relativePath) 
                projectList.addAll(getProjects(name, path))
            else {
                Project project = new Project(name, path)
                projectList.add(project)
            }
        }

        return projectList
    }

    private static ArrayList<Project> getProjects(String directoryName, String directoryPath) {
        ArrayList<Project> projectList = new ArrayList<Project>()

        File directory = new File(directoryPath)
        directory.traverse(type: DIRECTORIES, maxDepth: 0) {
             
             // Checking if it's a git project.
             String filePath = it.toString()
             if(new File("${filePath}/.git").exists()) {
                 Project project = new Project("${directoryName}/${it.getName()}", filePath)
                 projectList.add(project)
             }
        }

        return projectList
    }
} 